apply plugin: 'org.ajoberstar.grgit'

/**
 * Generates README.md based on project properties, and commits it
 */
task readme << {

  def updateReadme = {
    def text = new File('gradle/readme.template').getText('UTF-8')
    def template = new groovy.text.StreamingTemplateEngine().createTemplate(text)

    def binding = [
      version     : rootProject.version
    ]

    String newText = template.make(binding)
    newText = newText.replace('\\u007B', '{')

    def updated = false
    def readmeFile = new File('README.md')
    if (readmeFile.text != newText) {
      readmeFile.text = newText
      updated = true
    }
    return updated
  }

  def commitReadme = {
    grgit.add(patterns: ['README.md'])
    grgit.commit(message: ":books: Update README for ${rootProject.version}")
    grgit.push()
  }

  if (updateReadme()) {
    commitReadme()
    println "committed new README for ${rootProject.version}"
  } else {
    println "no changes to README for ${rootProject.version}"
  }
}
tasks.readme.description = 'Generates README.md based on project properties, and commits it.'
